ext.title = 'Oml Language'
description = 'Language infrastructure for Oml based on Xtext'

sourceSets {
	main {
		java {
			exclude '**/*.mwe2'
			exclude '**/*.xtext'
		}
	}
}

dependencies {
	testCompile "org.junit.jupiter:junit-jupiter-api:5.1.0"
	testRuntime "org.junit.jupiter:junit-jupiter-engine:5.1.0"
	testCompile "org.eclipse.xtext:org.eclipse.xtext.testing:${versions.xtext}"
	testCompile "org.eclipse.xtext:org.eclipse.xtext.xbase.testing:${versions.xtext}"
	compile project(':io.opencaesar.oml')
	compile "org.eclipse.xtext:org.eclipse.xtext:${versions.xtext}"
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase:${versions.xtext}"
	compile "com.google.code.gson:gson:${versions.gson}"
}

configurations {
	mwe2 {
		extendsFrom compile
	}
}

dependencies {
	mwe2 "org.eclipse.emf:org.eclipse.emf.mwe2.launch:${versions.mwe2}"
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:${versions.xtext}"
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.ecore:${versions.xtext}"
	mwe2 'org.eclipse.emf:org.eclipse.emf.ecore.xcore:1.3.+'
	mwe2 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:1.1.+'
	mwe2 'org.eclipse.emf:org.eclipse.emf.codegen.ecore.xtext:1.2.+'
}

task generateXtextLanguage(type: JavaExec) {
	main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
	classpath = configurations.mwe2
	inputs.file "src/main/java/io/opencaesar/oml/dsl/GenerateOml.mwe2"
	inputs.file "src/main/java/io/opencaesar/oml/dsl/Oml.xtext"
	outputs.dir "src/main/xtext-gen"
	args += "src/main/java/io/opencaesar/oml/dsl/GenerateOml.mwe2"
	args += "-p"
	args += "rootPath=/${projectDir}/.."
}

test {
	useJUnitPlatform()
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]
