module io.opencaesar.oml.dsl.GenerateOml

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		projectMapping = {
			path = "${rootPath}/io.opencaesar.oml"
			projectName = "io.opencaesar.oml"
		}
	}

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "io.opencaesar.oml.dsl"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				genericIde = {
					enabled = true
				}
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "io.opencaesar.oml.dsl.Oml"
			fileExtensions = "oml"
			referencedResource = "platform:/resource/io.opencaesar.oml/src/main/java/io/opencaesar/oml/Oml.xcore"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			generator = {
				generateStub = false
			}
			formatter = {
				generateStub = true
			}
			serializer = {
				generateStub = false
			}
			parserGenerator = {
				combinedGrammar = false
				debugGrammar = true
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				generateDeprecationValidation = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
