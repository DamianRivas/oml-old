/*
 * The OML package specifies the abstract syntax (metamodel) of the Ontology Modeling Language using the [Ecore](https://www.eclipse.org/modeling/emf/) language.
 * Therefore, OML models are instances of this OML package and can be manipulated using the Java API induced by this package.     
 */
@Ecore(
	nsPrefix="oml",
	nsURI="http://opencaesar.io/Oml"
)
@GenModel(
    bundleManifest="false",
    modelDirectory="io.opencaesar.oml/build/xcore/main", 
    complianceLevel="8.0"
)
@BikeshedGroups(
	Common="",
	Literals="",
	Graphs="",
	Terms="",
	ClassTerms="",
	DataTerms="",
	Axioms="",
	Rules="",
	Instances="",
	Assertions="",
	References="",
	TerminologyReferences="",
	DescriptionReferences=""
)
@Bikeshed(
	heading="Abstract Syntax"
)
package io.opencaesar.oml

annotation "http://opencaesar.io/Oml" as Oml
annotation "https://tabatkins.github.io/bikeshed/" as Bikeshed
annotation "https://tabatkins.github.io/bikeshed/groups" as BikeshedGroups

/*
 * Element is the most abstract superclass of all objects in an OML model.
 */ 
@Bikeshed(subsection="Common")
abstract class Element {
}

/*
 * Annotation is an Element that provides a value for an AnnotationProperty placed on an [=AnnotatedElement=]. It does not have DL semantics.
 */
@Bikeshed(subsection="Common")
class Annotation extends Element {
	/*
	 * The annotation property that has the value
	 */
	refers AnnotationProperty[1] property
	/*
	 * The value of the annotation property
	 */
	contains LiteralValue[1] value
}

/*
 * AnnotatedElement is an Element that may have a set of Annotations.
 */
@Bikeshed(subsection="Common")
abstract class AnnotatedElement extends Element {
	/*
	 * The set of annotations on this element
	 */
	contains Annotation[] annotations
}

/*
 * NamedElement is an AnnotatedElement that has a name.
 */
@Bikeshed(subsection="Common")
abstract class NamedElement extends AnnotatedElement {
	/*
	 * The name of the element
	 */
	String[1] name
}

/*
 * Graph is a NamedElement that represents the root of containment in an OML model. The name of the graph is an alias to its base IRI. 
 */
@Bikeshed(subsection="Graphs")
abstract class Graph extends NamedElement {
	/*
	 * The base IRI representing the namespace of the graph 
	 */
	String[1] iri
}

/*
 * Terminology is a Graph that contains TerminologyStatements and may import other Terminologies.
 */
@Bikeshed(subsection="Graphs")
class Terminology extends Graph {
	/*
	 * The kind of terminology
	 */
	@Oml(annotationProperty="true")
	TerminologyKind kind
	/*
	 * The set of direct imports of other terminologies
	 */
	contains TerminologyImport[] imports
	/*
	 * The set of statements in the terminology
	 */
	contains TerminologyStatement[] statements
}

/*
 * TerminologyKind is an enumeration of the kinds of Terminology.
 */
@Bikeshed(subsection="Graphs")
enum TerminologyKind {
	/*
	 * Specifies that the terminology has open-world semantics
	 */
	Open as "open"
	/*
	 * Specifies that the terminology has closed-world semantics
	 */
	Closed as "closed" = 1
}

/*
 * Description is a Graph that contains DescriptionStatements and may import other Descriptions.
 */
@Bikeshed(subsection="Graphs")
class Description extends Graph {
	/*
	 * The kind of description
	 */
	@Oml(annotationProperty="true")
	DescriptionKind kind
	/*
	 * The set of direct imports of other descriptions
	 */
	contains DescriptionImport[] imports
	/*
	 * The set of statements in the description
	 */
	contains DescriptionStatement[] statements
}

/*
 * DescriptionKind is an enumeration of the kinds of Description.
 */
@Bikeshed(subsection="Graphs")
enum DescriptionKind {
	/*
	 * Specifies that the description is final, i.e., cannot be refined further 
	 */
	Final as "final"
	/*
	 * Specifies that the description is partial, i.e., is expected to be refined further 
	 */
	Partial as "partial" = 1
}

/*
 * GraphMember is a NamedElement that is a member of a [=Graph=].
 */
@Bikeshed(subsection="Graphs")
abstract class GraphMember extends NamedElement {
}

/*
 * TerminologyMember is a GraphMember that is a member of a [=Terminology=].
 */
@Bikeshed(subsection="Graphs")
abstract class TerminologyMember extends GraphMember {
}

/*
 * Term is a TerminologyMember and TerminologyStatement that is defined by a [=Terminology=] and can be specialized in a hierarchy.
 */
@Bikeshed(subsection="Terms")
abstract class Term extends TerminologyMember, TerminologyStatement {
	/*
	 * The set of specializations of the term
	 */
	contains TermSpecializationAxiom[] specializations
}

/*
 * CharacterizableTerm is a Term that can be characterized by properties.
 */
@Bikeshed(subsection="Terms")
abstract class CharacterizableTerm extends Term {
}

/*
 * Entity is a CharacterizableTerm that can be interrelated and restricted.
 */
@Bikeshed(subsection="ClassTerms")
abstract class Entity extends CharacterizableTerm {
	/*
	 * The set of restriction axioms on the Entity's properties
	 */
	contains EntityRestrictionAxiom[] restrictions
}

/*
 * Aspect is an Entity that defines cross-cutting characterizations that can be mixed with those of other Entities.
 */
@Bikeshed(subsection="ClassTerms")
class Aspect extends Entity {
}

/*
 * Concept is an Entity that represents a concrete class of objects or ideas.
 */
@Bikeshed(subsection="ClassTerms")
class Concept extends Entity {
}

/*
 * ReifiedRelationship is a Relationship that is also reified as an Entity, i.e., can be characterized and interrelated.
 */
@Bikeshed(subsection="ClassTerms")
@Oml(reifiedRelationship="true", forward="reifiedRelationshipForward", inverse="reifiedRelationshipInverse")
class ReifiedRelationship extends Entity , Relationship {
}

/*
 * Structure is a CharacterizableTerm that can be specified by value (i.e., cannot be referenced).
 */
@Bikeshed(subsection="DataTerms")
class Structure extends CharacterizableTerm {
}

/*
 * Relationship is a Term that represents a relationship from a source Term to a target Term.
 */
@Bikeshed(subsection="ClassTerms")
abstract class Relationship extends Term {
	/*
	 * The source Term of the Relationship
	 */
	@Oml(source="true")
	refers Entity[1] source
	/*
	 * The target Term of the Relationship
	 */
	@Oml(target="true")
	refers Entity[1] target
	/*
	 * The forward direction of the relationship
	 */
	@Oml(ignore="true")
	contains ForwardDirection[1] forward
	/*
	 * The inverse direction of the relationship
	 */
	@Oml(ignore="true")
	contains InverseDirection inverse
	/*
	 * Whether the Relationship is functional (A->B and A->C => B=C)
	 */
	boolean functional
	/*
	 * Whether the Relationship is inverse functional (B->A and C->A => B=C)
	 */
	boolean inverseFunctional
	/*
	 * Whether the Relationship is symmetric (A->B => B->A)
	 */
	boolean symmetric
	/*
	 * Whether the Relationship is asymmetric (A->B => B!->A)
	 */
	boolean asymmetric
	/*
	 * Whether the Relationship is reflexive (A => A->A)
	 */
	boolean reflexive
	/*
	 * Whether the Relationship is reflexive (A => A!->A)
	 */
	boolean irreflexive
	/*
	 * Whether the Relationship is reflexive (A->B and B->C => A->C)
	 */
	boolean transitive
}

/*
 * UnreifiedRelationship is a Relationship that is not reified but represents a simple reference
 */
@Bikeshed(subsection="ClassTerms")
@Oml(reifiedRelationship="true", forward="unreifiedRelationshipForward", inverse="unreifiedRelationshipInverse")
class UnreifiedRelationship extends Relationship {
}

/*
 * ScalarRange is a Term that represents the abstract superclass of all ScalarRanges.
 */
@Bikeshed(subsection="DataTerms")
abstract class ScalarRange extends Term {
}

/*
 * Scalar is a ScalarRange that does not specialize other ScalarRanges.
 */
@Bikeshed(subsection="DataTerms")
class Scalar extends ScalarRange {
}

/*
 * CharArrayScalar is a ScalarRange that specializes another ScalarRange, whose literals are represented as character arrays,
 * and may restrict some facets of those arrays.
 */
@Bikeshed(subsection="DataTerms")
abstract class CharArrayScalar extends ScalarRange {
	/*
	 * The exact length of the character array 
	 */
	Integer length
	/*
	 * The minimum length of the character array 
	 */
	Integer minLength
	/*
	 * The maximum length of the character array 
	 */
	Integer maxLength
}

/*
 * BinaryScalar is a CharArrayScalar whose literals represent binary numbers, i.e., arrays of 0 and 1 digits.
 */
@Bikeshed(subsection="DataTerms")
class BinaryScalar extends CharArrayScalar {
}

/*
 * PatternScalar is a CharArrayScalar whose literals lexically conform to a pattern specified by a regex expression.
 */
@Bikeshed(subsection="DataTerms")
abstract class PatternScalar extends CharArrayScalar {
	/*
	 * The regex expression of the pattern
	 */
	String pattern
}

/*
 * IRIScalar is a PatternScalar whose literals represent IRIs.
 */
@Bikeshed(subsection="DataTerms")
class IRIScalar extends PatternScalar {
}

/*
 * PlainLiteralScalar is a PatternScalar whose literals belong to a given language.
 */
@Bikeshed(subsection="DataTerms")
class PlainLiteralScalar extends PatternScalar {
	/*
	 * The tag of the language of the literals. See this [reference](https://www.w3.org/TR/2012/REC-rdf-plain-literal-20121211/) for more details.
	 */
	String language
}

/*
 * StringScalar is a PatternScalar whose literals represent arbitrary string values.
 */
@Bikeshed(subsection="DataTerms")
class StringScalar extends PatternScalar {
}

/*
 * NumericScalar is a ScalarRange that specializes another ScalarRange, whose literals represent a numeric range,
 * and may restrict some facets of that range.
 */
@Bikeshed(subsection="DataTerms")
class NumericScalar extends ScalarRange {
	/*
	 * The minimum inclusive value of the numeric range
	 */
	contains LiteralNumber minInclusive
	/*
	 * The minimum exclusive value of the numeric range
	 */
	contains LiteralNumber minExclusive
	/*
	 * The maximum inclusive value of the numeric range
	 */
	contains LiteralNumber maxInclusive
	/*
	 * The maximum exclusive value of the numeric range
	 */
	contains LiteralNumber maxExclusive
}

/*
 * TimeScalar is a ScalarRange that specializes another ScalarRange, whose literals represent time range,
 * and may restrict some facets of that range.
 */
@Bikeshed(subsection="DataTerms")
class TimeScalar extends ScalarRange {
	/*
	 * The minimum inclusive value of the time range
	 */
	contains LiteralDateTime minInclusive
	/*
	 * The minimum exclusive value of the time range
	 */
	contains LiteralDateTime minExclusive
	/*
	 * The maximum inclusive value of the time range
	 */
	contains LiteralDateTime maxInclusive
	/*
	 * The maximum exclusive value of the time range
	 */
	contains LiteralDateTime maxExclusive
}

/*
 * EnumerationScalar is a ScalarRange whose literals are enumerated and cannot specialize other ScalarRanges.
 */
@Bikeshed(subsection="DataTerms")
class EnumerationScalar extends ScalarRange {
	/*
	 * The set of valid enumerated literals
	 */
	contains LiteralValue[] literals
}

/*
 * Property is a Term that adds an attribute to an Element. It is the abstract supertype of all Property types.
 */
@Bikeshed(subsection="Terms")
abstract class Property extends Term {
}

/*
 * CharacterizationProperty is a Property that adds an attribute with DL semantics to a [=CharacterizableTerm=].
 */
@Bikeshed(subsection="Terms")
abstract class CharacterizationProperty extends Property {
	/*
	 * The domain of the property that is characterized by it
	 */
	refers CharacterizableTerm[1] domain
	/*
	 * Whether the property is functional (has maximum one value)
	 */
	boolean functional
}

/*
 * StructuredProperty is a CharacterizationProperty whose values are instances of some Structure.
 */
@Bikeshed(subsection="Terms")
class StructuredProperty extends CharacterizationProperty {
	/*
	 * The structure that defines the range of the property
	 */
	refers Structure[1] range
}

/*
 * ScalarProperty is a CharacterizationProperty whose values are literals of some ScalarRange.
 */
@Bikeshed(subsection="Terms")
class ScalarProperty extends CharacterizationProperty {
	/*
	 * The scalar that defines the range of the property 
	 */
	refers ScalarRange[1] range
}

/*
 * AnnotationProperty is a Property that adds an attribute with no DL semantics to an [=AnnotatedElement=].
 */
@Bikeshed(subsection="Terms")
class AnnotationProperty extends Property {
}

/*
 * Rule is both a TerminologyMember and a TerminologyStatement and specifies additional inference logic beside the default provided by Description Logic. 
 */
@Bikeshed(subsection="Rules")
class Rule extends TerminologyMember, TerminologyStatement {
	/*
	 * The (ANDed) predicates that need to hold in order for the rule to infer the consequent
	 */
	contains Predicate[1..*] antecedent
	/*
	 * The predicate that is inferred when the antecedent predicates hold
	 */
	contains DirectionalRelationshipPredicate[1] consequent
}

/*
 * RelationshipDirection is a TerminologyMember that is owned by a [=Relationship] and specifies the characteristics (e.g., name) of one of its directions.
 */
@Bikeshed(subsection="ClassTerms")
abstract class RelationshipDirection extends TerminologyMember {
}

/*
 * ForwardDirection is a RelationshipDirection that characterizes the forward direction of a [=Relationship=].
 */
@Bikeshed(subsection="ClassTerms")
@Oml(ignore="true")
class ForwardDirection extends RelationshipDirection {
}

/*
 * InverseDirection is a RelationshipDirection that characterizes the inverse direction of a [=Relationship=].
 */
@Bikeshed(subsection="ClassTerms")
@Oml(ignore="true")
class InverseDirection extends RelationshipDirection {
}

/*
 * DescriptionMember is a GraphMember that is specified as a DescriptionStatement.  
 */
@Bikeshed(subsection="Graphs")
abstract class DescriptionMember extends GraphMember, DescriptionStatement {
}

/*
 * NamedInstance is a DescriptionMember that represents an Instance (of some [=Entity=]) with a name.
 */
@Bikeshed(subsection="Instances")
abstract class NamedInstance extends DescriptionMember , Instance {
}

/*
 * ConceptInstance is a NamedInstance that represents an instance of a [=Concept=].
 */
@Bikeshed(subsection="Instances")
class ConceptInstance extends NamedInstance {
	/*
	 * The set of type assertions for the instance
	 */
	contains ConceptInstanceTypeAssertion[] types
}

/*
 * ReifiedRelationshipInstance is a NamedInstance that represents an instance of a [=ReifiedRelationship=].
 */
@Bikeshed(subsection="Instances")
class ReifiedRelationshipInstance extends NamedInstance {
	/*
	 * The set of type assertions for the instance
	 */
	contains ReifiedRelationshipInstanceTypeAssertion[] types
	/*
	 * The source instance of the relationship instance
	 */
	refers NamedInstance[1] source
	/*
	 * The target instance of the relationship instance
	 */
	refers NamedInstance[1] target
}

/*
 * NamedElementReference is a AnnotatedElement that represents a reference to a [=NamedElement=].
 */
@Bikeshed(subsection="References")
abstract class NamedElementReference extends AnnotatedElement {
}

/*
 * GraphMemberReference is a NamedElementReference that represents a reference to a [=GraphMember=].
 */
@Bikeshed(subsection="References")
abstract class GraphMemberReference extends NamedElementReference {
}

/*
 * TerminologyMemberReference is a GraphMemberReference that represents a reference to a [=TerminologyMember=].
 */
@Bikeshed(subsection="TerminologyReferences")
abstract class TerminologyMemberReference extends GraphMemberReference, TerminologyStatement {
}

/*
 * TermReference is a TerminologyMemberReference that represents a reference to a [=Term=].
 */
@Bikeshed(subsection="TerminologyReferences")
abstract class TermReference extends TerminologyMemberReference {
	/*
	 * The set of specializations for the referenced term
	 */
	contains TermSpecializationAxiom[] specializations
}

/*
 * EntityReference is a TermReference that represents a reference to a [=Entity=].
 */
@Bikeshed(subsection="TerminologyReferences")
abstract class EntityReference extends TermReference {
	/*
	 * The set of restrictions for the referenced entity
	 */
	contains EntityRestrictionAxiom[] restrictions
}

/*
 * AspectReference is a EntityReference that represents a reference to an [=Aspect=].
 */
@Bikeshed(subsection="TerminologyReferences")
class AspectReference extends EntityReference {
	/*
	 * The referenced aspect
	 */
	refers Aspect[1] aspect
}

/*
 * ConceptReference is a EntityReference that represents a reference to a [=Concept=].
 */
@Bikeshed(subsection="TerminologyReferences")
class ConceptReference extends EntityReference {
	/*
	 * The referenced concept
	 */
	refers Concept[1] concept
}

/*
 * ReifiedRelationshipReference is a EntityReference that represents a reference to a [=ReifiedRelationship=].
 */
@Bikeshed(subsection="TerminologyReferences")
class ReifiedRelationshipReference extends EntityReference {
	/*
	 * The referenced reified relationship
	 */
	refers ReifiedRelationship[1] relationship
}

/*
 * UnreifiedRelationshipReference is a TermReference that represents a reference to a [=UnreifiedRelationship=].
 */
@Bikeshed(subsection="TerminologyReferences")
class UnreifiedRelationshipReference extends TermReference {
	/*
	 * The referenced unreified relationship
	 */
	refers UnreifiedRelationship[1] relationship
}

/*
 * StructureReference is a TermReference that represents a reference to a [=Structure=].
 */
@Bikeshed(subsection="TerminologyReferences")
class StructureReference extends TermReference {
	/*
	 * The referenced structure
	 */
	refers Structure[1] structure
}

/*
 * ScalarRangeReference is a TermReference that represents a reference to a [=ScalarRange=].
 */
@Bikeshed(subsection="TerminologyReferences")
class ScalarRangeReference extends TermReference {
	/*
	 * The referenced scalar
	 */
	refers ScalarRange[1] scalar
}

/*
 * StructuredPropertyReference is a TermReference that represents a reference to a [=StructuredProperty=].
 */
@Bikeshed(subsection="TerminologyReferences")
class StructuredPropertyReference extends TermReference {
	/*
	 * The referenced structured property
	 */
	refers StructuredProperty[1] property
}

/*
 * ScalarPropertyReference is a TermReference that represents a reference to a [=ScalarProperty=].
 */
@Bikeshed(subsection="TerminologyReferences")
class ScalarPropertyReference extends TermReference {
	/*
	 * The referenced scalar property
	 */
	refers ScalarProperty[1] property
}

/*
 * RelationshipDirectionReference is a TerminologyMemberReference that represents a reference to a [=RelationshipDirection=].
 */
@Bikeshed(subsection="TerminologyReferences")
class RelationshipDirectionReference extends TerminologyMemberReference {
	/*
	 * The referenced relationship direction
	 */
	refers RelationshipDirection[1] direction
}

/*
 * RuleReference is a TerminologyMemberReference that represents a reference to a [=Rule=].
 */
@Bikeshed(subsection="TerminologyReferences")
class RuleReference extends TerminologyMemberReference {
	/*
	 * The referenced rule
	 */
	refers Rule[1] rule	
}

/*
 * DescriptionMemberReference is a GraphMemberReference that represents a reference to a [=DescriptionMember=].
 */
@Bikeshed(subsection="DescriptionReferences")
abstract class DescriptionMemberReference extends GraphMemberReference, DescriptionStatement {
}

/*
 * NamedInstanceReference is a DescriptionMemberReference that represents a reference to a [=NamedInstance=].
 */
@Bikeshed(subsection="DescriptionReferences")
abstract class NamedInstanceReference extends DescriptionMemberReference {
	/*
	 * The set of property value assertions for the referenced instance
	 */
	contains InstancePropertyValueAssertion[] propertyValues
}

/*
 * ConceptInstanceReference is a NamedInstanceReference that represents a reference to a [=ConceptInstance=].
 */
@Bikeshed(subsection="DescriptionReferences")
class ConceptInstanceReference extends NamedInstanceReference {
	/*
	 * The referenced concept instance
	 */
	refers ConceptInstance[1] instance
	/*
	 * The set of type assertions for the referenced concept instance
	 */
	contains ConceptInstanceTypeAssertion[] types
}

/*
 * ReifiedRelationshipInstanceReference is a NamedInstanceReference that represents a reference to a [=ReifiedRelationshipInstance=].
 */
@Bikeshed(subsection="DescriptionReferences")
class ReifiedRelationshipInstanceReference extends NamedInstanceReference {
	/*
	 * The referenced reified relationship instance
	 */
	refers ReifiedRelationshipInstance[1] instance
	/*
	 * The set of type assertions for the referenced reified relationship instance
	 */
	contains ReifiedRelationshipInstanceTypeAssertion[] types
}

/**
 * GraphStatement is an AnnotatedElement that represents a statement owned by a [=Graph=].
 */
@Bikeshed(subsection="Graphs")
abstract class GraphStatement extends AnnotatedElement {
}

/*
 * TerminologyStatement is a GraphStatement that represents a statement owned by a [=Terminology=].
 */
@Bikeshed(subsection="Graphs")
abstract class TerminologyStatement extends GraphStatement {
}

/*
 * DescriptionStatement is a GraphStatement that represents a statement owned by a [=Description=].
 */
@Bikeshed(subsection="Graphs")
abstract class DescriptionStatement extends GraphStatement {
}

/*
 * Axiom is an AnnotatedElement that represents a statement owned by [=Term=].
 */
@Bikeshed(subsection="Axioms")
abstract class Axiom extends AnnotatedElement {
}

/*
 * TermSpecializationAxiom is an Axiom that represents a specialization by the owning [=Term=] of another [=Term=].
 */
@Bikeshed(subsection="Axioms")
class TermSpecializationAxiom extends Axiom {
	/*
	 * The term that is specialized by the axiom's owning term
	 */
	refers Term[1] specializedTerm
}

/*
 * EntityRestrictionAxiom is an Axiom owned by an [=Entity=] that restricts one of its properties or relationships
 */
@Bikeshed(subsection="Axioms")
abstract class EntityRestrictionAxiom extends Axiom {
}

/*
 * RelationshipRestrictionAxiom is an EntityRestrictionAxiom owned by an [=Entity=] that restricts one of its relationships.
 */
@Bikeshed(subsection="Axioms")
abstract class RelationshipRestrictionAxiom extends EntityRestrictionAxiom {
	/*
	 * The restricted relationship direction
	 */
	refers RelationshipDirection[1] relationshipDirection
	/*
	 * The entity that the relationship is restricted to
	 */
	refers Entity[1] restrictedTo
}

/*
 * ExistentialRelationshipRestrictionAxiom is an RelationshipRestrictionAxiom that specifies that there exists a relationship of the given kind from the owning [=Entity=] to the restricted [=Entity=].
 */
@Bikeshed(subsection="Axioms")
class ExistentialRelationshipRestrictionAxiom extends RelationshipRestrictionAxiom {
}

/*
 * UniversalRelationshipRestrictionAxiom is an RelationshipRestrictionAxiom that specifies that all relationships of the given kind from the owning [=Entity=] must be to the restricted [=Entity=]. 
 */
@Bikeshed(subsection="Axioms")
class UniversalRelationshipRestrictionAxiom extends RelationshipRestrictionAxiom {
}

@Bikeshed(subsection="Axioms")
abstract class ScalarPropertyRestrictionAxiom extends EntityRestrictionAxiom {
	refers ScalarProperty[1] property
}

@Bikeshed(subsection="Axioms")
abstract class TypedScalarPropertyRestrictionAxion extends ScalarPropertyRestrictionAxiom {
	refers ScalarRange[1] restrictedTo
}

@Bikeshed(subsection="Axioms")
class ExistentialScalarPropertyRestrictionAxiom extends TypedScalarPropertyRestrictionAxion {
}

@Bikeshed(subsection="Axioms")
class UniversalScalarPropertyRestrictionAxiom extends TypedScalarPropertyRestrictionAxion {
}

@Bikeshed(subsection="Axioms")
class ParticularScalarPropertyRestrictionAxiom extends ScalarPropertyRestrictionAxiom {
	contains LiteralValue[1] value
}

@Bikeshed(subsection="Axioms")
class StructuredPropertyRestrictionAxiom extends EntityRestrictionAxiom {
	refers StructuredProperty[1] property
	contains StructureInstance[1] value
}

@Bikeshed(subsection="Assertions")
abstract class Assertion extends AnnotatedElement {
}

@Bikeshed(subsection="Assertions")
class ConceptInstanceTypeAssertion extends Assertion {
	refers Concept[1] concept	
}

@Bikeshed(subsection="Assertions")
class ReifiedRelationshipInstanceTypeAssertion extends Assertion {
	refers ReifiedRelationship[1] relationship	
}

@Bikeshed(subsection="Assertions")
abstract class InstancePropertyValueAssertion extends Assertion {
}

@Bikeshed(subsection="Assertions")
class ScalarPropertyValueAssertion extends InstancePropertyValueAssertion {
	refers ScalarProperty[1] property
	contains LiteralValue[1] value
}

@Bikeshed(subsection="Assertions")
class StructuredPropertyValueAssertion extends InstancePropertyValueAssertion {
	refers StructuredProperty[1] property
	contains StructureInstance[1] value
}

@Bikeshed(subsection="Graphs")
abstract class GraphImport extends Element {
	String[1] importURI
	String importedNamespace
}

@Bikeshed(subsection="Graphs")
abstract class TerminologyImport extends GraphImport {
}

@Bikeshed(subsection="Graphs")
class TerminologyExtension extends TerminologyImport {
}

@Bikeshed(subsection="Graphs")
abstract class DescriptionImport extends GraphImport {
}

@Bikeshed(subsection="Graphs")
class DescriptionUsage extends DescriptionImport {
}

@Bikeshed(subsection="Graphs")
class DescriptionRefinement extends DescriptionImport {
}

@Bikeshed(subsection="Instances")
abstract class Instance extends Element {
	contains InstancePropertyValueAssertion[] propertyValues
}

@Bikeshed(subsection="Instances")
class StructureInstance extends Instance {
	refers Structure[1] structure
}

@Bikeshed(subsection="Literals")
abstract class LiteralValue extends Element {
	refers ScalarRange valueType
}

@Bikeshed(subsection="Literals")
class LiteralBoolean extends LiteralValue {
	boolean value
}

@Bikeshed(subsection="Literals")
class LiteralDateTime extends LiteralValue {
	String[1] value
}

@Bikeshed(subsection="Literals")
class LiteralString extends LiteralValue {
	String[1] value
}

@Bikeshed(subsection="Literals")
class LiteralUUID extends LiteralValue {
	String[1] value
}

@Bikeshed(subsection="Literals")
class LiteralURI extends LiteralValue {
	String[1] value
}

@Bikeshed(subsection="Literals")
abstract class LiteralNumber extends LiteralValue {
}

@Bikeshed(subsection="Literals")
class LiteralReal extends LiteralNumber {
	Double[1] value
}

@Bikeshed(subsection="Literals")
class LiteralRational extends LiteralNumber {
	Double[1] value
}

@Bikeshed(subsection="Literals")
class LiteralFloat extends LiteralNumber {
	Float[1] value
}

@Bikeshed(subsection="Literals")
class LiteralDecimal extends LiteralNumber {
	BigDecimal[1] value
}

@Bikeshed(subsection="Rules")
abstract class Predicate extends Element {
}

@Bikeshed(subsection="Rules")
class EntityPredicate extends Predicate {
	refers Entity[1] entity
	String[1] variable
}

@Bikeshed(subsection="Rules")
abstract class RelationshipPredicate extends Predicate {
	String[1] variable1
	String[1] variable2
}

@Bikeshed(subsection="Rules")
class DirectionalRelationshipPredicate extends RelationshipPredicate {
	refers RelationshipDirection[1] relationshipDirection
}

@Bikeshed(subsection="Rules")
class ReifiedRelationshipPredicate extends RelationshipPredicate {
	refers ReifiedRelationship[1] relationship
	ReifiedRelationshipPredicateKind kind
}

@Bikeshed(subsection="Rules")
enum ReifiedRelationshipPredicateKind {
	Source as "source"
	InverseSource as "inverseSource" = 1
	Target as "target" = 2
	InverseTarget as "inverseTarget" = 3
}

